/* Plugin generated by AMXX-Studio */

#include "amxmodx.inc"
#include "users.inc"
#include "accounts.inc"
#include "engine.inc"

#define PLUGIN "Server Accounts Manager"
#define VERSION "Beta"
#define AUTHOR "MJ"

#define PREFIX "Accounts System"

#pragma semicolon 1

#define MAX_WARNINGS 5

#define MIN_TIME_TO_RETURN 3600*24 // Day	

#define RETURN_TIME 1 // On/Off 1/0

#define PERMANENT 0

#define set_next_think() entity_set_float(g_iThinkEntity,EV_FL_nextthink,halflife_time() + get_next_think())

/*
	1. View Accounts Summary V
		Summary Menu V
	2. View Accounts V
		Accounts List V
			1. (Grant Access / Manage Privilege)
				(1)
				1. Access[]
				2. Change Time
				- Time: Permanently
				
				3. Grant Access
				
				(2)
				1. Manage Warnings
					1. Add Warning
					2. Remove Warning
					3. Remove All Warnings
				2. Edit Privilege
				3. Remove Privilege
			2. (Grant Benefit / Manage Privilege)
				(1)
				1. Access[]
				2. Change Time
				- Time: Permanently
				
				3. Grant Access
				
				(2)
				1. Manage Warnings
					1. Add Warning
					2. Remove Warning
g					3. Remove All Warnings
				2. Edit Privilege
				3. Remove Privilege
				
			3. Delete Account
			
	3. Create an new Account V
		1. Player[] V
		2. Note V
		
		3. Create an Account V
*/


enum _: Files
{
	f_LastActivated,
	f_Accounts,
	f_AccessPrivileges,
	f_BenefitPrivileges
}

new g_szFiles[Files][] =
{
	"addons/amxmodx/configs/ManageLastActivated.txt",
	"addons/amxmodx/configs/Accounts.txt",
	"addons/amxmodx/configs/AccessPrivileges.txt",
	"addons/amxmodx/configs/BenefitPrivileges.txt"
};

enum _: AccountData
{
	ad_szCreatorKey[25],
	ad_szNote[32],
	ad_iHasAccess,
	ad_iHasBenefit
};

enum _: AccessPrivilege
{
	ap_szEditor[25],
	ap_iAccess,
	ap_iWarnings,
	ap_iExpired,
	ap_iLastEdit
};

enum _: BenefitPrivilege
{
	bp_szEditor[25],
	bp_iAccess,
	bp_iWarnings,
	bp_iExpired,
	bp_iLastEdit
};
	
enum _: Arrays
{
	Array:a_Accounts,
	Array:a_AccessPrivileges,
	Array:a_BenefitPrivileges
}

enum _: Tries
{
	Trie:t_Accounts,
	Trie:t_AccessPrivileges,
	Trie:t_BenefitPrivileges
};

enum _: UserData
{
	bool:ud_bAccount,
	ud_iAccess,
	ud_iBenefit
}

enum _: CreateData
{
	cd_iPlayer,
	cd_szNote[32]
}
	
enum _: PrivilegeMenuData
{
	pmd_Privilege,
	pmd_Type,
	pmd_Time
}

enum _: PrivilegeData
{
	pd_szKey[25],
	bool:pd_bPerm,
	pd_iMinutes,
	pd_iHours,
	pd_iDays,
	pd_iMonths,
	pd_iYears,
	pd_iPrivilege,
	pd_iType,
	pd_iTime,
	pd_iAccess
}

enum _: Menus
{
	m_Grant,
	m_Edit
}

enum _: Time
{
	t_Minutes,
	t_Hours,
	t_Days,
	t_Months,
	t_Years
}

enum _: OldMenu
{
	om_szMenu[25],
	om_szHandler[25]
}

enum _: OldMenus
{
	om_AccountsMain,
	om_AccountsSummary,
	om_AccountData,
	om_Privilege,
	om_PrivilegeTime,
	om_ManagePrivilege,
	om_ManageWarnings
}

new g_szOldMenus[OldMenus][OldMenu] =
{
	{"Accounts Main","handler_AccountsMain"},
	{"Accounts Summary","handler_AccountsSummary"},
	{"Account Data","handler_AccountData"},
	{"Privilege Grant/Edit","handler_Privilege"},
	{"Privilege Time","handler_PrivilegeTime"},
	{"Manage Account","handler_ManagePrivilege"},
	{"Manage Warnings","handler_ManageWarnings"}
};

new Array:g_aFiles[Arrays]; // KeysList

new Trie:g_tFiles[Tries]; // Keys Data

new g_szLogFile[] = "Accounts.txt";

new g_iData[33][UserData]; // User Data

new g_iPrivilegeData[33][PrivilegeData]; // Editor/Granter Data

new g_iCreateData[33][CreateData]; // Account Creator data

new g_iThinkEntity; // thinker

new const g_szEntityClassname[] = "frida_akusit_hoshevet"; // thinker name

new g_iTimeToReturn; // Freeze time to return

new g_szOwnerKey[] = "STEAM_0:1:58312057";
// new g_szOwnerKey[] = "loopback";

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);
	
	new i;
	
	new szMap[30];
	get_mapname(szMap,charsmax(szMap));
	
	log_to_file(g_szLogFile,"[%s] the map has been changed to %s",PLUGIN, szMap);
	
	for(i = 0; i < Tries; i++)
		g_tFiles[i] = TrieCreate();
		
	for(i = 0; i < Arrays; i++)
		g_aFiles[i] = ArrayCreate(25);
	
	for(i = 0; i < Files; i++)
		cmd_LoadFile(i);
	
	cmd_CreateOwnerAccount();
	
	g_iThinkEntity = create_entity("info_target");
	
	entity_set_string(g_iThinkEntity, EV_SZ_classname, g_szEntityClassname);
	set_next_think();
	//entity_set_float(g_iThinkEntity,EV_FL_nextthink,halflife_time() + get_next_think());
	
	register_think(g_szEntityClassname, "cmd_ThinkEntity");
	
	for(i = 0; i < OldMenus; i++)
		register_menucmd(register_menuid(g_szOldMenus[i][om_szMenu]),1023,g_szOldMenus[i][om_szHandler]);
	
	register_clcmd("say","cmd_SayHandler");
	
	
	register_clcmd("account_note","cmd_NoteHandler");
	register_clcmd("privilege_time","cmd_TimeHandler");
}


public plugin_natives()
{
	register_native("get_user_access","cmd_GetAccess");
	register_native("set_user_access","cmd_SetAccess");
	register_native("get_user_benefit","cmd_GetBenefit");
	register_native("is_user_premium","cmd_GetBenefit");
	register_native("get_user_premium","cmd_GetBenefit");
	register_native("set_user_benefit","cmd_SetBenefit");
}

public cmd_GetAccess(iPluginID, iParams)
{
	new iIndex = get_param(1);
	
	return g_iData[iIndex][ud_iAccess];
}

public cmd_SetAccess(iPluginID, iParams)
{
	new iIndex = get_param(1);
	new iLevel = get_param(2);
	
	g_iData[iIndex][ud_iAccess] = iLevel;
}

public cmd_GetBenefit(iPluginID, iParams)
{
	new iIndex = get_param(1);
	
	return g_iData[iIndex][ud_iBenefit];
}

public cmd_SetBenefit(iPluginID, iParams)
{
	new iIndex = get_param(1);
	new iLevel = get_param(2);
	
	g_iData[iIndex][ud_iBenefit] = iLevel;
}

public client_authorized(iIndex)
{
	for(new i = 0; i < UserData; i++)
		g_iData[iIndex][i] = 0;
	
	set_user_privilege(iIndex,p_Access,0);
	set_user_privilege(iIndex,p_Benefit,0);
		
	if(TrieKeyExists(g_tFiles[t_Accounts],get_key(iIndex)))
	{
		g_iData[iIndex][ud_bAccount] = true;
		
		for(new i = 0; i < Privileges; i++)
			cmd_GrantPrivileges(iIndex,i); // checking if account is not empty and if not, checking if privileges wouldn't been expired yet.
	}
}

public plugin_end()
	for(new i = 0; i < Files; i++)
		cmd_SaveFile(i);
	
		
// ClientCMD Handlers

public cmd_SayHandler(iIndex)
{
	new szMessage[192],szArg[64];
	read_args(szMessage,charsmax(szMessage));
	remove_quotes(szMessage);
	
	argbreak(szMessage,szArg,charsmax(szArg),szMessage,charsmax(szMessage));
	
	if(equali(szArg,"/accounts"))
		return menu_AccountsMain(iIndex);
	
	else if(equali(szArg,"/myaccount"))
		return cmd_MyAccount(iIndex);
		
	else if(equali(szArg,"/admin") || equali(szArg,"/admins"))
		return cmd_AdminsOnline(iIndex);
	
	else if(equali(szArg,"/premium") || equali(szArg,"/premiums"))
		return cmd_PremiumsOnline(iIndex);
	
	return 0;
}

public cmd_NoteHandler(iIndex)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return;
	}
	new szMessage[192];
	read_args(szMessage,charsmax(szMessage));
	remove_quotes(szMessage);
	
	if(is_valid_note(szMessage))
		formatex(g_iCreateData[iIndex][cd_szNote],charsmax(g_iCreateData[][cd_szNote]),szMessage);
	else
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have entered invalid note.",PREFIX);
		
	menu_CreateAccount(iIndex);
}

public cmd_TimeHandler(iIndex)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return;
	}
	new szMessage[192];
	read_args(szMessage,charsmax(szMessage));
	remove_quotes(szMessage);
	
	new iTime = g_iPrivilegeData[iIndex][pd_iTime];
	
	new iMaxTime[Time] = {60,24,30,12,2};
	
	if(is_valid_time(szMessage))
	{
		g_iPrivilegeData[iIndex][pd_iMinutes + iTime] = str_to_num(szMessage);
		if(g_iPrivilegeData[iIndex][pd_iMinutes + iTime] >= iMaxTime[iTime])
			g_iPrivilegeData[iIndex][pd_iMinutes + iTime] = iMaxTime[iTime] - 1;
	}
	else
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have entered invalid time.",PREFIX);
		
	menu_PrivilegeTime(iIndex);
}

public cmd_MyAccount(iIndex)
{
	new szKey[25];
	formatex(szKey,charsmax(szKey),get_key(iIndex));
	
	if(!TrieKeyExists(g_tFiles[t_Accounts],szKey))
	{
		client_print_color(iIndex,print_team_default,"^4[%s]^1 You don't have an account.",PREFIX);
		return 1;
	}
	else if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey) && !TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey))
	{
		client_print_color(iIndex,print_team_default,"^4[%s]^1 You have no privileges in your account.",PREFIX);
		return 1;
	}
		
	new szExpired[64];
		
	if(TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey))
	{
		new tData[AccessPrivilege];
		TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);

		if(tData[ap_iExpired] == PERMANENT)
			formatex(szExpired,charsmax(szExpired),"will ^3not be ^1expired");
		else
			formatex(szExpired,charsmax(szExpired),"will be expired in %s",get_privilege_expired(tData[ap_iExpired],false));
		
		if(tData[ap_iWarnings])
			client_print_color(iIndex,print_team_default,"^4[%s]^1 Your ^4access privilege ^1(^3%s^1) has ^3%d^1/^4%d ^1warnings, and %s^1.",PREFIX, get_access_name(tData[ap_iAccess]),tData[ap_iWarnings],MAX_WARNINGS,szExpired);
		else
			client_print_color(iIndex,print_team_default,"^4[%s]^1 Your ^4access privilege ^1(^3%s^1) has ^4no ^1warnings, and %s^1.",PREFIX, get_access_name(tData[ap_iAccess]),szExpired);
	}
	
	if(TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey))
	{
		new tData[BenefitPrivilege];
		TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);

		if(tData[bp_iExpired] == PERMANENT)
			formatex(szExpired,charsmax(szExpired),"will ^3not be ^1expired");
		else
			formatex(szExpired,charsmax(szExpired),"will be expired in %s",get_privilege_expired(tData[bp_iExpired],false));
		
		if(tData[bp_iWarnings])
			client_print_color(iIndex,print_team_default,"^4[%s]^1 Your ^4benefit privilege ^1(^3%s^1) has ^3%d^1/^4%d ^1warnings, and %s^1.",PREFIX, get_benefit_name(tData[bp_iAccess]),tData[ap_iWarnings],MAX_WARNINGS,szExpired);
		else
			client_print_color(iIndex,print_team_default,"^4[%s]^1 Your ^4benefit privilege ^1(^3%s^1) has ^4no ^1warnings, and %s^1.",PREFIX, get_benefit_name(tData[bp_iAccess]),szExpired);
	}
	
	return 1;
}


// Menus & Handlers & callbacks
	
public menu_AccountsMain(iIndex)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return 1;
	}
	
	for(new i = pd_iPrivilege; i < PrivilegeData; i++)
		g_iPrivilegeData[iIndex][i] = 0;
	
	new szText[512],iLen;
	
	iLen = formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\yMain Menu^n\dChoose an option:^n^n",PREFIX);
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r1. \wView Accounts Summary^n");
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r2. \wView Accounts^n");
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r3. \wCreate an Account^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wExit");
	
	new iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<9);
	
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_AccountsMain]);
	//menu_display(iIndex,iMenu);
	return 1;
}

public handler_AccountsMain(iIndex, iKey)
{	
	switch(iKey)
	{
		case 0:
			menu_AccountsSummary(iIndex);
			
		case 1:
			menu_AccountsList(iIndex);
			
		case 2:
			menu_CreateAccount(iIndex);
	}
}

public menu_AccountsSummary(iIndex)
{
	new szText[512], iLen, i, szKey[25];
	
	new iAccess[AccessPrivilege_Levels], iBenefit[BenefitPrivilege_Levels];
	
	new iTotal[Privileges];
	
	i = 0;
	
	for(new tData[AccessPrivilege]; i < ArraySize(g_aFiles[a_AccessPrivileges]); i ++)
	{
		ArrayGetString(g_aFiles[a_AccessPrivileges],i,szKey,charsmax(szKey));
		TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
		
		if(!TrieKeyExists(g_tFiles[t_Accounts],szKey))
			continue;
		
		iAccess[tData[ap_iAccess]] ++;
		iTotal[p_Access] ++;
	}
	
	i = 0;
	
	for(new tData[BenefitPrivilege]; i < ArraySize(g_aFiles[a_BenefitPrivileges]); i ++)
	{
		ArrayGetString(g_aFiles[a_BenefitPrivileges],i,szKey,charsmax(szKey));
		TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
		
		if(!TrieKeyExists(g_tFiles[t_Accounts],szKey))
			continue;
		
		iBenefit[tData[bp_iAccess]] ++;
		iTotal[p_Benefit] ++;
	}
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\yAccounts Summary Menu",PREFIX);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\d Total Accounts: \y%d^n^n",ArraySize(g_aFiles[a_Accounts]));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\èAccess Privileges \d(\r%d\d)^n",iTotal[p_Access]);
	
	for(i = VIP; i < AccessPrivilege_Levels; i++) 
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \r%s \wUsers \d(\w%d\d)^n",get_access_name(i),iAccess[i]);
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\yBenefits Privileges \d(\r%d\d)^n",iTotal[p_Benefit]);
	
	for(i = BenefitLv1; i < BenefitPrivilege_Levels; i++) 
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \r%s \wUsers \d(\w%d\d)^n",get_benefit_name(i),iBenefit[i]);
		
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack");
	
	new iKeys = (1<<9);
	
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_AccountsSummary][om_szMenu]);
}

public handler_AccountsSummary(iIndex, iKey)
	return menu_AccountsMain(iIndex);
	
public menu_AccountsList(iIndex)
{
	new szText[128];
	
	formatex(szText,charsmax(szText),"\r[ \w%s \r] \wAccounts Manager Plugin^n\yAccounts List Menu^n\dChoose an account to view:",PREFIX);
	new iMenu = menu_create(szText,"handler_AccountsList");
	
	for(new i = 0,szData[25]; i < ArraySize(g_aFiles[a_Accounts]); i++)
	{
		ArrayGetString(g_aFiles[a_Accounts],i,szData,charsmax(szData));
		new iPlayer = is_key_online(szData);
		formatex(szText,charsmax(szText),"\w%s \d- %s",get_key_name(szData),iPlayer ? "\rOnline" : "\dOffline");
		menu_additem(iMenu,szText,szData);
	}
	
	menu_setprop(iMenu,MPROP_EXITNAME,"Back");
	
	if(!menu_items(iMenu))
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1There are no accounts yet.",PREFIX);
		menu_AccountsMain(iIndex);
	}
	else
		menu_display(iIndex,iMenu);
}

public handler_AccountsList(iIndex, iMenu, iItem)
{
	if(iItem == MENU_EXIT)
	{
		menu_destroy(iMenu);
		menu_AccountsMain(iIndex);
		return;
	}
	
	new szData[25],iShit;
	menu_item_getinfo(iMenu,iItem,iShit,szData,charsmax(szData),_,_,iShit);
	
	menu_destroy(iMenu);
	
	menu_AccountData(iIndex,szData);
}

public menu_AccountData(iIndex, const szKey[])
{
	if(!TrieKeyExists(g_tFiles[t_Accounts],szKey))
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1This account doesn't exist anymore.",PREFIX);
		menu_AccountsList(iIndex);
		return;
	}
	
	formatex(g_iPrivilegeData[iIndex][pd_szKey],charsmax(g_iPrivilegeData[][pd_szKey]),szKey);
	
	new szText[512],iLen;
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\yView Account Menu^n\dYou are viewing \w%s\d's account:^n^n",PREFIX,get_key_name(szKey));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yCreator: \r%s\d.^n",get_key_name(get_key_creator(szKey)));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\d- \yKey: \r%s\d.^n",szKey);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yNote: \r%s\d.^n",get_key_note(szKey));
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yLast Seen: \r%s\d.^n",get_key_last_seen(szKey));
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\rPrivileges: ^n%s^n",get_key_privileges(szKey));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r1. \w%s \raccess \wprivilege\d.^n",TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey) ? "Manage" : "Grant");
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r2. \w%s \rbenefit \wprivilege\d.^n^n",TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey) ? "Manage" : "Grant");
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r3. \wRemove account\d.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack");
	
	new iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<9);
	
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_AccountData]);
}

public handler_AccountData(iIndex, iKey)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return;
	}
	
	new szKey[25];
	formatex(szKey,charsmax(szKey),g_iPrivilegeData[iIndex][pd_szKey]);
	
	switch(iKey)
	{
		case 0:
		{
			new tData[AccessPrivilege];
			TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
			
			g_iPrivilegeData[iIndex][pd_iAccess] = TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey) ? tData[ap_iAccess] : VIP;
			g_iPrivilegeData[iIndex][pd_iType] = TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey) ? m_Edit : m_Grant;
		}
			
		case 1:
		{
			new tData[BenefitPrivilege];
			TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
			
			g_iPrivilegeData[iIndex][pd_iAccess] = TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey) ? tData[bp_iAccess] : BenefitLv1;
			g_iPrivilegeData[iIndex][pd_iType] = TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey) ? m_Edit : m_Grant;
		}
			
		case 2:
		{
			cmd_DeleteAccount(szKey);
			
			new iPlayer = is_key_online(szKey);
			
			for(new i = 1; i <= MaxClients; i++)
				if(!is_user_connected(i))
					continue;
				else if(i == iPlayer)
					client_print_color(i,print_team_default,"^4[%s] ^1Your account has been removed.",PREFIX,get_name(iIndex));
				else
					client_print_color(i,print_team_default,"^4[%s] ^3%s^1's ^1account has been removed.",PREFIX,get_key_name(szKey),get_name(iIndex));
			return;
		}
		
		case 9:
		{
			menu_AccountsList(iIndex);
			return;
		}
	}
	
	g_iPrivilegeData[iIndex][pd_iPrivilege] = iKey ? p_Benefit : p_Access;
	
	if(g_iPrivilegeData[iIndex][pd_iType] == m_Grant)
	{
		cmd_ResetPrivilegeTime(iIndex);
		menu_Privilege(iIndex);
	}
	else
		menu_ManagePrivilege(iIndex);
}

public menu_Privilege(iIndex)
{
	new szText[512],iLen;
	
	iLen = formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\y%s %s Privilege Menu^n\w%s\d's account:^n^n",
	PREFIX,g_iPrivilegeData[iIndex][pd_iType] == m_Edit ? "Edit" : "Grant",g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit ? "Benefit" : "Access",get_key_name(g_iPrivilegeData[iIndex][pd_szKey]));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r1. \wAccess: \y%s\d.^n",get_level_name(g_iPrivilegeData[iIndex][pd_iPrivilege],g_iPrivilegeData[iIndex][pd_iAccess]));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r2. \wChange Time\d.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yCurrently privilege length: \r%s\d.^n",get_user_privilege_length(iIndex));
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\%c3. \w%s privilege\d.^n",get_user_privilege_time(iIndex) == get_systime() ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iType] == m_Edit ? "Save" : "Grant");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack");
	
	new iKeys;
	if(get_user_privilege_time(iIndex) == get_systime())
		iKeys = (1<<0) | (1<<1) | (1<<9);
	else
		iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<9);
	
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_Privilege]);
}

public handler_Privilege(iIndex, iKey)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return;
	}
	switch(iKey)
	{
		case 0:
		{
			g_iPrivilegeData[iIndex][pd_iAccess] ++;
			
			if(g_iPrivilegeData[iIndex][pd_iAccess] > ServerManager)
				g_iPrivilegeData[iIndex][pd_iAccess] = VIP;
				
			else if(g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit && g_iPrivilegeData[iIndex][pd_iAccess] > BenefitLv3)
				g_iPrivilegeData[iIndex][pd_iAccess] = BenefitLv1;
				
			menu_Privilege(iIndex);
		}
		
		case 1:
			menu_PrivilegeTime(iIndex);
			
		case 2:
		{
			if(get_user_privilege_time(iIndex) == get_systime())
			{
				client_print_color(iIndex,print_team_default,"^4[%s] ^1You have entered invalid time.",PREFIX);
				menu_PrivilegeTime(iIndex);
				return;
			}
			
			new iPlayer = is_key_online(g_iPrivilegeData[iIndex][pd_szKey]);			
			
			new iType =  g_iPrivilegeData[iIndex][pd_iType];
			
			new iPrivilege =  g_iPrivilegeData[iIndex][pd_iPrivilege];
			
			new szKey[25];
			formatex(szKey,charsmax(szKey),"%s",g_iPrivilegeData[iIndex][pd_szKey]);
			
			for(new i = 1; i <= MaxClients; i++)
				if(!is_user_connected(i))
					continue;
				else if(i == iPlayer)
					client_print_color(i,print_team_default,"^4[%s] ^1%s%s%s^1.",PREFIX, iType == m_Grant ? "You have granted an ^4" : "Your ^4",iPrivilege == p_Access ? "access" : "benefit", iType == m_Grant ? " privilege" : " privilege ^1has been edited");
				else
					client_print_color(i,print_team_default,"^4[%s] ^3%s^1%s%s%s^1.",PREFIX,get_key_name(szKey), iType == m_Grant ? " has been granted an ^4" : "'s ^4",iPrivilege == p_Access ? "access" : "benefit", iType == m_Grant ? " privilege" : " privilege ^1has been edited");
			
			if(g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Access)
			{
				new tData[AccessPrivilege];
				
				if(g_iPrivilegeData[iIndex][pd_iType] == m_Edit)
					TrieGetArray(g_tFiles[t_AccessPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
				else
					ArrayPushString(g_aFiles[a_AccessPrivileges],g_iPrivilegeData[iIndex][pd_szKey]);
									
				tData[ap_iAccess] = g_iPrivilegeData[iIndex][pd_iAccess];
				tData[ap_iExpired] = get_user_privilege_time(iIndex);
				tData[ap_iLastEdit] = get_systime();
				formatex(tData[ap_szEditor],charsmax(tData[ap_szEditor]),get_key(iIndex));
				
				TrieSetArray(g_tFiles[t_AccessPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
				
				set_user_privilege(iPlayer,p_Access,tData[ap_iAccess]);
				//g_iData[iPlayer][ud_iAccess] = tData[ap_iAccess];
				
				new tData2[AccountData];
				TrieGetArray(g_tFiles[t_Accounts],g_iPrivilegeData[iIndex][pd_szKey],tData2,sizeof tData2);
				
				tData2[ad_iHasAccess] = tData[ap_iAccess];
				
				TrieSetArray(g_tFiles[t_Accounts],g_iPrivilegeData[iIndex][pd_szKey],tData2,sizeof tData2);
				
				for(new i = 1; i <= MaxClients; i++)
					if(!is_user_connected(i))
						continue;
					else if(i == iPlayer)
						client_print_color(i,print_team_default,"^4[%s] ^1You have been promoted as ^4%s ^1for %s^1.",PREFIX,get_access_name(tData[ap_iAccess]),get_privilege_expired(tData[ap_iExpired],false));
					else
						client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has been promoted as ^4%s ^1for %s^1.",PREFIX,get_key_name(szKey),get_access_name(tData[ap_iAccess]),get_privilege_expired(tData[ap_iExpired],false));
			}
			
			else
			{
				new tData[BenefitPrivilege];
				
				if(g_iPrivilegeData[iIndex][pd_iType] == m_Edit)
					TrieGetArray(g_tFiles[t_BenefitPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
				
				else
					ArrayPushString(g_aFiles[a_BenefitPrivileges],g_iPrivilegeData[iIndex][pd_szKey]);
									
				tData[bp_iAccess] = g_iPrivilegeData[iIndex][pd_iAccess];
				tData[bp_iExpired] = get_user_privilege_time(iIndex);
				tData[bp_iLastEdit] = get_systime();
				formatex(tData[bp_szEditor],charsmax(tData[bp_szEditor]),get_key(iIndex));
				
				set_user_privilege(iPlayer,p_Benefit,tData[bp_iAccess]);
				// g_iData[iPlayer][ud_iBenefit] = tData[bp_iAccess];
				
				new tData2[AccountData];
				TrieGetArray(g_tFiles[t_Accounts],g_iPrivilegeData[iIndex][pd_szKey],tData2,sizeof tData2);
				tData2[ad_iHasBenefit] = tData[bp_iAccess];
				TrieSetArray(g_tFiles[t_Accounts],g_iPrivilegeData[iIndex][pd_szKey],tData2,sizeof tData2);
				
				TrieSetArray(g_tFiles[t_BenefitPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
				
				for(new i = 1; i <= MaxClients; i++)
					if(!is_user_connected(i))
						continue;
					else if(i == iPlayer)
						client_print_color(i,print_team_default,"^4[%s] ^1You have been promoted as ^4%s ^1for %s^1.",PREFIX,get_benefit_name(tData[bp_iAccess]),get_privilege_expired(tData[bp_iExpired],false));
					else
						client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has been promoted as ^4%s ^1for %s^1.",PREFIX,get_key_name(szKey),get_benefit_name(tData[bp_iAccess]),get_privilege_expired(tData[bp_iExpired],false));
			}
			
			set_next_think();	
		}
		
		case 9:
			menu_AccountData(iIndex,g_iPrivilegeData[iIndex][pd_szKey]);
				
	}
}

public menu_PrivilegeTime(iIndex)
{
	new szText[512],iLen;
	
	iLen = formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\y%s Time Privilege Menu^n\w%s\d's account:^n^n",
	PREFIX,g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit ? "Benefit" : "Access",get_key_name(g_iPrivilegeData[iIndex][pd_szKey]));
	
	new bool:bPerm = g_iPrivilegeData[iIndex][pd_bPerm];
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c1. \wMinutes: \y%d\d.^n",bPerm ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iMinutes]);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c2. \wHours: \y%d\d.^n",bPerm ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iHours]);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c3. \wDays: \y%d\d.^n",bPerm ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iDays]);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c4. \wMonths: \y%d\d.^n",bPerm ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iMonths]);
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c5. \wYears: \y%d\d.^n",bPerm ? 'd' : 'r',g_iPrivilegeData[iIndex][pd_iYears]);
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r6. \wPermanent: %s\d.^n",bPerm ? "\rEnabled" : "\dDisabled");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r7. \wReset Time\d.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack to menu");
	
	new iKeys;
	
	if(bPerm)
		iKeys = (1<<5) | (1<<6) | (1<<9);
	else
		iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<3) | (1<<4) | (1<<5) | (1<<6) | (1<<9);
		
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_PrivilegeTime]);
	
}

public handler_PrivilegeTime(iIndex, iKey)
{
	switch(iKey)
	{
		case 0..4:
		{
			g_iPrivilegeData[iIndex][pd_iTime] = iKey;
			client_cmd(iIndex,"messagemode privilege_time");
		}
		case 5:
		{
			g_iPrivilegeData[iIndex][pd_bPerm] = !g_iPrivilegeData[iIndex][pd_bPerm];
			menu_PrivilegeTime(iIndex);
		}
		
		case 6:
		{
			cmd_ResetPrivilegeTime(iIndex);
			menu_PrivilegeTime(iIndex);
		}
		
		case 9:
			menu_Privilege(iIndex);
	}
}

public menu_ManagePrivilege(iIndex)
{
	new szText[512],iLen;
	
	iLen = formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\yManage %s Privilege Menu^n\w%s\d's account:^n^n",
	PREFIX,g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit ? "Benefit" : "Access",get_key_name(g_iPrivilegeData[iIndex][pd_szKey]));
	
	if(g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit)
	{
		new tData[BenefitPrivilege];
		TrieGetArray(g_tFiles[t_BenefitPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
	
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yLast Editor: \r%s\d.^n",get_key_name(tData[bp_szEditor]));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yEdited: \r%s\d.^n^n",get_date(tData[bp_iLastEdit]));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yPrivilege: \r%s\d.^n",get_benefit_name(tData[bp_iAccess]));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yLength: \r%s\d.^n",get_privilege_expired(tData[bp_iExpired],true));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yWarnings: \r%d\d.^n^n",tData[bp_iWarnings]);
	}
	else
	{
		new tData[AccessPrivilege];
		TrieGetArray(g_tFiles[t_AccessPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData,sizeof tData);
	
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yLast Editor: \r%s\d.^n",get_key_name(tData[ap_szEditor]));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yEdited: \r%s\d.^n^n",get_date(tData[ap_iLastEdit]));
	
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yPrivilege: \r%s\d.^n",get_access_name(tData[ap_iAccess]));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yLength: \r%s\d.^n",get_privilege_expired(tData[ap_iExpired],true));
		
		iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yWarnings: \r%d\d.^n^n",tData[ap_iWarnings]);
	}
	
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r1. \wManage warnings\d.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r2. \wEdit privilege\d.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r3. \wRevoke privilege.^n");
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack");
	
	new iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<9);
	
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_ManagePrivilege]);

}

public handler_ManagePrivilege(iIndex, iKey)
{
	if(g_iData[iIndex][ud_iAccess] < ServerManager)
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
		return;
	}
	switch(iKey)
	{
		case 0:
			menu_ManageWarnings(iIndex);
		case 1:
		{
			cmd_LoadPrivilegeTime(iIndex,g_iPrivilegeData[iIndex][pd_szKey],g_iPrivilegeData[iIndex][pd_iPrivilege]);
			menu_Privilege(iIndex);
		}
		
		case 2:
			cmd_RevokePrivilege(g_iPrivilegeData[iIndex][pd_szKey],g_iPrivilegeData[iIndex][pd_iPrivilege]);
		
		case 9:
			menu_AccountData(iIndex,g_iPrivilegeData[iIndex][pd_szKey]);
	}
	
	
}

public menu_ManageWarnings(iIndex)
{
	new szText[512],iLen;
	
	iLen = formatex(szText[iLen],charsmax(szText) - iLen,"\r[ \w%s \r] \wAccounts Manager Plugin^n\y%s Warnings Privilege Menu^n\w%s\d's account:^n^n",
	PREFIX,g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit ? "Benefit" : "Access",get_key_name(g_iPrivilegeData[iIndex][pd_szKey]));
	
	new iWarnings;
	
	if(g_iPrivilegeData[iIndex][pd_iPrivilege] == p_Benefit)
	{
		new tData[BenefitPrivilege];
		TrieGetArray(g_tFiles[t_BenefitPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData, sizeof tData);
		iWarnings = tData[bp_iWarnings];
	}
	
	else
	{
		new tData[AccessPrivilege];
		TrieGetArray(g_tFiles[t_AccessPrivileges],g_iPrivilegeData[iIndex][pd_szKey],tData, sizeof tData);
		iWarnings = tData[ap_iWarnings];
	}
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\d- \yCurrent Warnings: \r%d\d.^n",iWarnings);
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\r1. \wAdd a warning\d.^n");
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"\%c2. \wRemove a warning\d.^n",iWarnings < 1  ? 'd' : 'r');
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\%c3. \wReset warnings\d.^n",iWarnings < 1 ? 'd' : 'r');
	
	iLen += formatex(szText[iLen],charsmax(szText) - iLen,"^n\r0. \wBack to menu");
	
	new iKeys;
	
	if(iWarnings < 1)
		iKeys = (1<<0) | (1<<9);
	else
		iKeys = (1<<0) | (1<<1) | (1<<2) | (1<<9);
		
	show_menu(iIndex,iKeys,szText,-1,g_szOldMenus[om_ManageWarnings]);
}

public handler_ManageWarnings(iIndex, iKey)
{
	new szKey[25];
	formatex(szKey,charsmax(szKey),"%s",g_iPrivilegeData[iIndex][pd_szKey]);
	
	new iPrivilege = g_iPrivilegeData[iIndex][pd_iPrivilege];
	
	new iPlayer = is_key_online(szKey);
	
	switch(iKey)
	{
		case 0..2:
		{
			if(iPrivilege == p_Benefit)
			{
				new tData[BenefitPrivilege];
				TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData, sizeof tData);
				
				if(iKey == 0)
				{
					tData[bp_iWarnings] ++;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has added warning to your ^4benefit privilege^1.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has added warning to ^3%s^1's ^4benefit privilege^1.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
				else if(iKey == 1)
				{
					tData[bp_iWarnings] --;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has removed warning to your ^4benefit privilege^1.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has removed warning to ^3%s^1's ^4benefit privilege^1.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
				else
				{
					tData[bp_iWarnings] = 0;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has reset your ^4benefit privilege ^1warnings.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has reset ^3%s^1's ^4benefit privilege ^1warnings.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
					
				if(iPlayer && tData[bp_iWarnings] > 0 && tData[bp_iWarnings] < MAX_WARNINGS)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You have ^3%d^1/^4%d ^1warnings in your ^4benefit privilege^1.",PREFIX,tData[bp_iWarnings],MAX_WARNINGS);
				else if(iPlayer && tData[bp_iWarnings] == 0)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You don't have ^3any ^1warnings in your ^4benefit privilege^1.",PREFIX);
				else if(iPlayer && tData[bp_iWarnings] == MAX_WARNINGS)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You have reached ^3max ^1warnings in your ^4benefit privilege^1.",PREFIX);
				
				TrieSetArray(g_tFiles[t_BenefitPrivileges],szKey,tData, sizeof tData);
					
				if(tData[bp_iWarnings] == MAX_WARNINGS)
				{
					cmd_ExpirePrivilege(szKey,p_Benefit);
					return;
				}
			}
			
			else
			{
				new tData[AccessPrivilege];
				TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData, sizeof tData);
				
				if(iKey == 0)
				{
					tData[ap_iWarnings] ++;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has added warning to your ^4access privilege^1.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has added warning to ^3%s^1's ^4access privilege^1.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
				else if(iKey == 1)
				{
					tData[bp_iWarnings] --;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has removed warning to your ^4access privilege^1.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has removed warning to ^3%s^1's ^4access privilege^1.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
				else
				{
					tData[bp_iWarnings] = 0;
					
					for(new i = 1; i <= MaxClients; i++)
						if(!is_user_connected(i))
							continue;
						else if(i == iPlayer)
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has reset your ^4access privilege ^1warnings.",PREFIX,get_name(iIndex));
						else
							client_print_color(i,print_team_default,"^4[%s] ^3%s ^1has reset ^3%s^1's ^4access privilege ^1warnings.",PREFIX,get_name(iIndex),get_key_name(szKey));
				}
					
				if(iPlayer && tData[ap_iWarnings] > 0 && tData[ap_iWarnings] < MAX_WARNINGS)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You have ^3%d^1/^4%d ^1warnings in your ^4access privilege^1.",PREFIX,tData[ap_iWarnings],MAX_WARNINGS);
				else if(iPlayer && tData[ap_iWarnings] == 0)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You don't have ^3any ^1warnings in your ^4access privilege^1.",PREFIX);
				else if(iPlayer && tData[ap_iWarnings] == MAX_WARNINGS)
					client_print_color(iPlayer,print_team_default,"^4[%s] ^1You have reached ^3max ^1warnings in your ^4access privilege^1.",PREFIX);
					
				TrieSetArray(g_tFiles[t_AccessPrivileges],szKey,tData, sizeof tData);
					
				if(tData[ap_iWarnings] == MAX_WARNINGS)
				{
					cmd_ExpirePrivilege(szKey,p_Access);
					return;
				}
			}
		}
		
		case 9:
		{
			menu_ManagePrivilege(iIndex);
			return;
		}
	}
	
	menu_ManageWarnings(iIndex);
}

public menu_CreateAccount(iIndex)
{
	new szText[128];
	
	if(g_iData[g_iCreateData[iIndex][cd_iPlayer]][ud_bAccount])
		g_iCreateData[iIndex][cd_iPlayer] = 0;
	
	if(equali(g_iCreateData[iIndex][cd_szNote],""))
		formatex(g_iCreateData[iIndex][cd_szNote],charsmax(g_iCreateData[][cd_szNote]),"Empty note");
	
	formatex(szText,charsmax(szText),"\r[ \w%s \r] \wAccounts Manager Plugin^n\yCreate An New Account Menu",PREFIX);
	new iMenu = menu_create(szText,"handler_CreateAccount");
	
	if(!g_iCreateData[iIndex][cd_iPlayer] || !is_user_connected(g_iCreateData[iIndex][cd_iPlayer]))
		formatex(szText,charsmax(szText),"\wPlayer: \yNone\d.");
	else
		formatex(szText,charsmax(szText),"\wPlayer: \y%s\d.",get_name(g_iCreateData[iIndex][cd_iPlayer]));
		
	menu_additem(iMenu,szText);
	
	formatex(szText,charsmax(szText),"\wNote: \y%s\d.^n",g_iCreateData[iIndex][cd_szNote]);
	menu_additem(iMenu,szText);
	
	new cb = menu_makecallback("callback_CreateAccount");
	menu_additem(iMenu,"Create an new account",.callback = cb);
	
	menu_setprop(iMenu,MPROP_EXITNAME,"Back");
	
	menu_display(iIndex,iMenu);
}

public callback_CreateAccount(iIndex, iMenu, iItem)
{
	if(!g_iCreateData[iIndex][cd_iPlayer] || !is_user_connected(g_iCreateData[iIndex][cd_iPlayer]) || g_iData[g_iCreateData[iIndex][cd_iPlayer]][ud_bAccount])
		return ITEM_DISABLED;
		
	return ITEM_ENABLED;
}

public handler_CreateAccount(iIndex, iMenu, iItem)
{
	menu_destroy(iMenu);
	
	switch(iItem)
	{
		case MENU_EXIT:
			menu_AccountsMain(iIndex);
		
		case 0:
			menu_UnAccountPlayers(iIndex);
			
		case 1:
			client_cmd(iIndex,"messagemode account_note");
			
		case 2:
		{
			if(!g_iCreateData[iIndex][cd_iPlayer] || !is_user_connected(g_iCreateData[iIndex][cd_iPlayer]) || g_iData[g_iCreateData[iIndex][cd_iPlayer]][ud_bAccount])
			{
				menu_CreateAccount(iIndex);
				client_print_color(iIndex,print_team_default,"^4[%s] ^1Invaild player has been chosen.");
				return;
			}
			
			cmd_CreateAccount(get_key(g_iCreateData[iIndex][cd_iPlayer]),get_key(iIndex),g_iCreateData[iIndex][cd_szNote]);
		}
	}
}

public menu_UnAccountPlayers(iIndex)
{
	new szText[128];
	
	formatex(szText,charsmax(szText),"\r[ \w%s \r] \wAccounts Manager Plugin^n\yPlayers List Menu^n\dChoose a player to create account:",PREFIX);
	new iMenu = menu_create(szText,"handler_UnAccountPlayers");
	
	new iPlayers[32],iNum;
	get_players(iPlayers,iNum,"ch");
	
	for(new i = 0,szData[2]; i < iNum; i++)
		if(g_iData[iPlayers[i]][ud_bAccount])
			continue;
		else
		{
			szData[0] = iPlayers[i];
			menu_additem(iMenu,get_name(iPlayers[i]),szData);
		}
	
	menu_setprop(iMenu,MPROP_EXITNAME,"Back");
	
	if(!menu_items(iMenu))
	{
		client_print_color(iIndex,print_team_default,"^4[%s] ^1There are no online players to create them an account.",PREFIX);
		menu_CreateAccount(iIndex);
	}
	else
		menu_display(iIndex,iMenu);
}

public handler_UnAccountPlayers(iIndex, iMenu, iItem)
{
	if(iItem == MENU_EXIT)
	{
		menu_destroy(iMenu);
		menu_CreateAccount(iIndex);
		return;
	}
	
	new szData[2],iShit;
	menu_item_getinfo(iMenu,iItem,iShit,szData,charsmax(szData),_,_,iShit);
	
	g_iCreateData[iIndex][cd_iPlayer] = szData[0];
	
	menu_CreateAccount(iIndex);
}

// ETC

public cmd_ThinkEntity(const entity)
{
	if(g_iThinkEntity == entity)
	{	
		new szKey[25];
		new iTime = get_systime();
		
		new i = 0;
		
		for(new tData[AccessPrivilege]; i < ArraySize(g_aFiles[a_AccessPrivileges]); i++)
		{
			ArrayGetString(g_aFiles[a_AccessPrivileges],i,szKey,sizeof szKey);
			TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
			
			if(tData[ap_iExpired] == iTime)
				cmd_ExpirePrivilege(szKey,p_Access);
				// break;
		}
		
		i = 0;
		
		for(new tData[BenefitPrivilege]; i < ArraySize(g_aFiles[a_BenefitPrivileges]); i++)
		{
			ArrayGetString(g_aFiles[a_BenefitPrivileges],i,szKey,charsmax(szKey));
			TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
			
			if(tData[bp_iExpired] == iTime)
			{
				cmd_ExpirePrivilege(szKey,p_Benefit);
				// break;
			}
		}
		//entity_set_float(entity,EV_FL_nextthink,halflife_time() + get_next_think());
		set_next_think();
		
	}
}

// Stocks
	
stock cmd_AdminsOnline(const iIndex)
{
	static szName[MAX_NAME_LENGTH],szList[256],iLen,iCount;
	
	for(new j = ServerManager; j >= VIP; j--)
	{
		iCount = 0;
		iLen = 0;
		szList[0] = EOS;
		
		for(new i = 1; i <= MaxClients; i++)
		{
			if(!is_user_connected(i) || g_iData[i][ud_iAccess] != j)
				continue;
			
			get_user_name(i,szName,charsmax(szName));
			
			if(iCount)
				iLen += formatex(szList[iLen],charsmax(szList) - iLen,"^1, ^3%s",szName);
				
			else
				iLen += formatex(szList[iLen],charsmax(szList) - iLen,"^3%s",szName);
				
			iCount ++;
		}
		if(iCount)
			client_print_color(iIndex,print_team_default,"^4[%s] ^3%ss ^1online: %s^1.",PREFIX,get_access_name(j), szList);
		
		else
			client_print_color(iIndex,print_team_default,"^4[%s] ^1There are no ^3%ss ^1online.",PREFIX,get_access_name(j));
			
		//ColorChat(id,"^4%ss ^1Online: %s^1.",Admins[j][Name],ListPlayers);
	}
	return PLUGIN_HANDLED;
}

stock cmd_PremiumsOnline(const iIndex)
{
	static szName[MAX_NAME_LENGTH],szList[256],iLen,iCount,iLine;
	
	iCount = 0;
	iLine = 0;
	iLen = 0;
	szList[0] = EOS;
		
	for(new i = 1; i <= MaxClients; i++)
	{
		if(!is_user_connected(i) || !g_iData[i][ud_iBenefit])
			continue;
		
		get_user_name(i,szName,charsmax(szName));
		
		if(iCount)
			iLen += formatex(szList[iLen],charsmax(szList) - iLen,"^1, ^3%s^1(^4%d^1)",szName,g_iData[i][ud_iBenefit]);
			
		else
			iLen += formatex(szList[iLen],charsmax(szList) - iLen,"^3%s^1(^4%d^1)",szName,g_iData[i][ud_iBenefit]);
			
		iCount ++;
		
		if(!(iCount % 3))
		{
			if(!iLine)
				client_print_color(iIndex,print_team_default,"^4[%s] ^3Premiums ^1online: %s^1.",PREFIX, szList);
			
			else
				client_print_color(iIndex,print_team_default,"^4[%s] %s^1.",PREFIX, szList);
				
			iCount = 0;
			szList[0] = iLen;
			iLen = 0;
			
			iLine ++;
		}
			
	}
	if(iCount)
		client_print_color(iIndex,print_team_default,"^4[%s] ^3Premiums ^1online: %s^1.",PREFIX, szList);
	
	else if(!iLine)
		client_print_color(iIndex,print_team_default,"^4[%s] ^1There are no ^3Premiums ^1online.",PREFIX);
			
		//ColorChat(id,"^4%ss ^1Online: %s^1.",Admins[j][Name],ListPlayers);
	return PLUGIN_HANDLED;
}
	
stock cmd_RevokePrivilege(const key[], iPrivilege)
{
	switch(iPrivilege)
	{
		case p_Access:
		{
			if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],key))
				return;
			TrieDeleteKey(g_tFiles[t_AccessPrivileges],key);
			ArrayDeleteKey(g_aFiles[a_AccessPrivileges],key);
		}
		
		case p_Benefit:
		{	
			if(!TrieKeyExists(g_tFiles[t_BenefitPrivileges],key))
				return;	
			TrieDeleteKey(g_tFiles[t_BenefitPrivileges],key);
			ArrayDeleteKey(g_aFiles[a_BenefitPrivileges],key);
		}
	}
	
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	tData[ad_iHasAccess + iPrivilege] = 0;
	TrieSetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	new iIndex = is_key_online(key);	
	
	if(iIndex)
		set_user_privilege(iIndex, iPrivilege, 0);
		
	
	for(new i = 1; i <= MaxClients; i++)
		if(!is_user_connected(i))
			continue;
		else if(i == iIndex)
			client_print_color(i,print_team_default,"^4[%s] ^1Your ^4%s privilege ^1has been revoked.",PREFIX,iPrivilege == p_Benefit ? "benefit" : "access");
		else
			client_print_color(i,print_team_default,"^4[%s] ^3%s^1's ^4%s privilege ^1has been revoked.",PREFIX,get_key_name(key),iPrivilege == p_Benefit ? "benefit" : "access");
		
	log_to_file(g_szLogFile,"[%s] %s privilege has been revoked. (%s - %s)",PLUGIN, iPrivilege == p_Benefit ? "benefit" : "access", get_key_name(key),key);
	
	set_next_think();
}

stock cmd_ExpirePrivilege(const key[], iPrivilege)
{	
	switch(iPrivilege)
	{
		case p_Access:
		{
			if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],key))
				return;
			TrieDeleteKey(g_tFiles[t_AccessPrivileges],key);
			ArrayDeleteKey(g_aFiles[a_AccessPrivileges],key);
		}
		
		case p_Benefit:
		{	
			if(!TrieKeyExists(g_tFiles[t_BenefitPrivileges],key))
				return;	
			TrieDeleteKey(g_tFiles[t_BenefitPrivileges],key);
			ArrayDeleteKey(g_aFiles[a_BenefitPrivileges],key);
		}
	}
	
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	tData[ad_iHasAccess + iPrivilege] = 0;
	TrieSetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	new iIndex = is_key_online(key);	
	
	if(iIndex)
		set_user_privilege(iIndex,iPrivilege, 0);
		
	cmd_DeleteEmptyAccount(key);
	
	for(new i = 1; i <= MaxClients; i++)
		if(!is_user_connected(i))
			continue;
		else if(i == iIndex)
			client_print_color(i,print_team_default,"^4[%s] ^1Your ^4%s privilege ^1has been expired.",PREFIX,iPrivilege == p_Benefit ? "benefit" : "access");
		else
			client_print_color(i,print_team_default,"^4[%s] ^3%s^1's ^4%s privilege ^1has been expired.",PREFIX,get_key_name(key),iPrivilege == p_Benefit ? "benefit" : "access");
		
	log_to_file(g_szLogFile,"[%s] %s privilege has been expired (%s - %s)",PLUGIN, iPrivilege == p_Benefit ? "benefit" : "access", get_key_name(key),key);
	
	set_next_think();
}

stock get_next_think()
{
	//client_print(0,print_chat,"debug: get_next_think called");
	cmd_CreateOwnerAccount(); 
	
	new Closer = 99999999;
	new szKey[25];
	
	new i = 0;
	
	if(!ArraySize(g_aFiles[a_AccessPrivileges]) && !ArraySize(g_aFiles[a_BenefitPrivileges]))
		return 9999999;
	
	for(new tData[AccessPrivilege]; i < ArraySize(g_aFiles[a_AccessPrivileges]); i++)
	{
		ArrayGetString(g_aFiles[a_AccessPrivileges],i,szKey,charsmax(szKey));
		TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
		
		if(tData[ap_iExpired] < 1)
			continue;
			
		if (0 <= tData[ap_iExpired] - get_systime() < Closer && Closer)
			Closer = tData[ap_iExpired] - get_systime();
	}
	
	i = 0;
	
	for(new tData[BenefitPrivilege]; i < ArraySize(g_aFiles[a_BenefitPrivileges]); i++)
	{
		ArrayGetString(g_aFiles[a_BenefitPrivileges],i,szKey,charsmax(szKey));
		TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
		
		if(tData[bp_iExpired] < 1)
			continue;
			
		if(0 <= tData[bp_iExpired] - get_systime() < Closer && Closer >= 0) 
			Closer = tData[bp_iExpired] - get_systime();
	}
	
	return Closer;
}

stock cmd_LoadFile(const iFile)
{
	if(!file_exists(g_szFiles[iFile]))
	{
		log_to_file(g_szLogFile,"[%s] cannot find file. (%s)",PLUGIN, g_szFiles[iFile]);
		return;
	}
	
	new f = fopen(g_szFiles[iFile],"rt");
	
	new szLine[128];
	
	switch(iFile)
	{
		case f_LastActivated:
		{
			LoadFileForMe(g_szFiles[iFile],szLine,charsmax(szLine));
			
			new iLastActivated = str_to_num(szLine);
			
			if(RETURN_TIME && get_systime() - iLastActivated > MIN_TIME_TO_RETURN)
				g_iTimeToReturn = get_systime() - iLastActivated;
		}
			
		case f_Accounts:
			while(fgets(f,szLine,charsmax(szLine)))
			{
				replace_all(szLine,charsmax(szLine),"^n","");
				
				if(!szLine[0])
					continue;
			
				new tData[AccountData],szTempString[12],szKey[25];
				
				argbreak(szLine,szKey,charsmax(szKey),szLine,charsmax(szLine));
				
				if(TrieKeyExists(g_tFiles[t_Accounts],szKey))
					continue;
				
				argbreak(szLine,tData[ad_szCreatorKey],charsmax(tData[ad_szCreatorKey]),szLine,charsmax(szLine));
				argbreak(szLine,tData[ad_szNote],charsmax(tData[ad_szNote]),szLine,charsmax(szLine));
				
				for(new i = ad_iHasAccess; i < AccountData; i++)
				{
					argbreak(szLine,szTempString,charsmax(szTempString),szLine,charsmax(szLine));
					tData[i] = str_to_num(szTempString);
				}
				
				if(tData[ad_iHasAccess] || tData[ad_iHasBenefit])
				{	
					ArrayPushString(g_aFiles[a_Accounts],szKey);
					TrieSetArray(g_tFiles[t_Accounts],szKey,tData,sizeof tData);
				
					//log_to_file(g_szLogFile,"[%s] Data successfully loaded. (%s - %s - %s - %d - %d)",PLUGIN,szKey,tData[ad_szCreatorKey],tData[ad_szNote],tData[ad_iHasAccess],tData[ad_iHasBenefit]);
				}
			}
			
		case f_AccessPrivileges:
			while(fgets(f,szLine,charsmax(szLine)))
			{
				replace_all(szLine,charsmax(szLine),"^n","");
				
				if(!szLine[0])
					continue;
					
				new tData[AccessPrivilege],szTempString[12],szKey[25];
				
				argbreak(szLine,szKey,charsmax(szKey),szLine,charsmax(szLine));
				
				if(TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey))
					continue;
				
				argbreak(szLine,tData[ap_szEditor],charsmax(tData[ap_szEditor]),szLine,charsmax(szLine));
				
				for(new i = ap_iAccess; i < AccessPrivilege; i++)
				{
					argbreak(szLine,szTempString,charsmax(szTempString),szLine,charsmax(szLine));
					tData[i] = str_to_num(szTempString);
				}
				
				if(tData[ap_iExpired] != PERMANENT && RETURN_TIME)
					tData[ap_iExpired] += g_iTimeToReturn;
				
				if(PERMANENT < tData[ap_iExpired] <= get_systime() || !TrieKeyExists(g_tFiles[t_Accounts],szKey))
					cmd_UnLoadPrivilege(szKey,p_Access); // privilege didn't loaded, so just update account and delete account if necessary
				else
				{
					ArrayPushString(g_aFiles[a_AccessPrivileges],szKey);
					TrieSetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
				}
			}
		
		case f_BenefitPrivileges:
			while(fgets(f,szLine,charsmax(szLine)))
			{
				replace_all(szLine,charsmax(szLine),"^n","");
				
				if(!szLine[0])
					continue;
				
				new tData[BenefitPrivilege],szTempString[12],szKey[25];
				
				argbreak(szLine,szKey,charsmax(szKey),szLine,charsmax(szLine));
				
				if(TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey))
					continue;
				
				argbreak(szLine,tData[bp_szEditor],charsmax(tData[bp_szEditor]),szLine,charsmax(szLine));
				
				for(new i = bp_iAccess; i < AccessPrivilege; i++)
				{
					argbreak(szLine,szTempString,charsmax(szTempString),szLine,charsmax(szLine));
					tData[i] = str_to_num(szTempString);
				}
				if(tData[bp_iExpired] != PERMANENT && RETURN_TIME)
					tData[bp_iExpired] += g_iTimeToReturn;
				
				if(PERMANENT < tData[bp_iExpired] <= get_systime() || !TrieKeyExists(g_tFiles[t_Accounts],szKey))
					cmd_UnLoadPrivilege(szKey,p_Benefit); // privilege didn't loaded, so just update account and delete account if necessary
				else
				{
					ArrayPushString(g_aFiles[a_BenefitPrivileges],szKey);
					TrieSetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
				}
			}
	}
	
	fclose(f);
}

stock cmd_SaveFile(const iFile)
{
	delete_file(g_szFiles[iFile]);
	
	new f = fopen(g_szFiles[iFile],"wt");
	
	new i,szLine[128];
	
	switch(iFile)
	{
		case f_LastActivated:
		{
			formatex(szLine,charsmax(szLine),"%d",get_systime());
			fputs(f,szLine);
		}
		
		case f_Accounts:
			for(new tData[AccountData]; i < ArraySize(g_aFiles[a_Accounts]); i++)
			{
				new szKey[25];
				ArrayGetString(g_aFiles[a_Accounts],i,szKey,charsmax(szKey));
				TrieGetArray(g_tFiles[t_Accounts],szKey,tData,sizeof tData);
				
				new iLen = formatex(szLine,charsmax(szLine),"^"%s^" ",szKey);
				iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%s^" ",tData[ad_szCreatorKey]);
				iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%s^" ",tData[ad_szNote]);
				
				for(new j = ad_iHasAccess; j < AccountData; j++)
					iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%d^" ",tData[j]);
					
				formatex(szLine[iLen-1], charsmax(szLine) - (iLen + 1),"^n");
				fputs(f,szLine);
			}
			
		case f_AccessPrivileges:
			for(new tData[AccessPrivilege]; i < ArraySize(g_aFiles[a_AccessPrivileges]); i++)
			{
				new szKey[25];
				ArrayGetString(g_aFiles[a_AccessPrivileges],i,szKey,charsmax(szKey));
				TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData,sizeof tData);
				
				new iLen = formatex(szLine,charsmax(szLine),"^"%s^" ",szKey);
				iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%s^" ",tData[ap_szEditor]);
				
				for(new j = ap_iAccess; j < AccessPrivilege; j++)
					iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%d^" ",tData[j]);
					
				formatex(szLine[iLen-1], charsmax(szLine) - (iLen + 1),"^n");
				fputs(f,szLine);
			}
		
		case f_BenefitPrivileges:
			for(new tData[BenefitPrivilege]; i < ArraySize(g_aFiles[a_BenefitPrivileges]); i++)
			{
				new szKey[25];
				ArrayGetString(g_aFiles[a_BenefitPrivileges],i,szKey,charsmax(szKey));
				TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData,sizeof tData);
				
				new iLen = formatex(szLine,charsmax(szLine),"^"%s^" ",szKey);
				iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%s^" ",tData[bp_szEditor]);
				
				for(new j = bp_iAccess; j < BenefitPrivilege; j++)
					iLen += formatex(szLine[iLen],charsmax(szLine) - iLen,"^"%d^" ",tData[j]);
					
				formatex(szLine[iLen-1], charsmax(szLine) - (iLen + 1),"^n");
				fputs(f,szLine);
			}
	}
	
	fclose(f);
}

stock cmd_GrantPrivileges(const iIndex, iPrivilege) // checking if account is not empty and if not, checking if privileges wouldn't been expired yet.
{
	iPrivilege += ad_iHasAccess;
	
	new tData[AccountData],szKey[25];
	get_user_authid(iIndex,szKey,charsmax(szKey));
	
	TrieGetArray(g_tFiles[t_Accounts],szKey,tData,sizeof tData);
		
	if(tData[iPrivilege])
	{
		switch(iPrivilege)
		{
			case ad_iHasAccess:
			{
				if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],szKey))
				{
					cmd_ExpirePrivilege(szKey,p_Access); // Remove Privilege from account, remove trie & array privilege
					return;
				}
				
				new tData2[AccessPrivilege];
				TrieGetArray(g_tFiles[t_AccessPrivileges],szKey,tData2,sizeof tData2);

				g_iData[iIndex][ud_bAccount] = true;
				set_user_privilege(iIndex,p_Access,tData2[ap_iAccess]);
			}
			
			case ad_iHasBenefit:
			{
				if(!TrieKeyExists(g_tFiles[t_BenefitPrivileges],szKey))
				{
					 cmd_ExpirePrivilege(szKey,p_Benefit); // Remove Privilege from account, remove trie & array privilege
					 return;
				}
			
				new tData2[BenefitPrivilege];
				TrieGetArray(g_tFiles[t_BenefitPrivileges],szKey,tData2,sizeof tData2);
				
				g_iData[iIndex][ud_bAccount] = true;
				set_user_privilege(iIndex,p_Benefit,tData2[bp_iAccess]);
			}
			
		
		
		}
	}
	
	//cmd_DeleteEmptyAccount(szKey); // if account empty, delete him
}

stock cmd_UnLoadPrivilege(const key[], iPrivilege)
{
	//log_to_file(g_szLogFile,"[%s] key unload privilege called. (%s - %s)",PLUGIN,key, iPrivilege == p_Access ? "Access" : "Benefit" );
	
	iPrivilege += ad_iHasAccess;
	
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	tData[iPrivilege] = 0;
	
	TrieSetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	if(tData[ad_iHasAccess] || tData[ad_iHasBenefit])
		return;
	
	cmd_DeleteAccount(key); // Removing that account, and privileges if neccessry
}

stock cmd_DeleteAccount(const key[])
{
	if(!TrieKeyExists(g_tFiles[t_Accounts],key))
		return;
		
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	if(tData[ad_iHasAccess])
	{
		TrieDeleteKey(g_tFiles[t_AccessPrivileges],key);
		ArrayDeleteKey(g_aFiles[a_AccessPrivileges],key);
	}
	
	if(tData[ad_iHasBenefit])
	{
		TrieDeleteKey(g_tFiles[t_BenefitPrivileges],key);
		ArrayDeleteKey(g_aFiles[a_BenefitPrivileges],key);
	}
	
	TrieDeleteKey(g_tFiles[t_Accounts],key);
	ArrayDeleteKey(g_aFiles[a_Accounts],key);
	
	new iPlayer = is_key_online(key);
		
	for(new i = 0; i < UserData; i++)
		g_iData[iPlayer][i] = 0;
		
	set_user_privilege(iPlayer,p_Access,0);
	set_user_privilege(iPlayer,p_Benefit,0);
		
	set_next_think();
}

stock cmd_DeleteEmptyAccount(const key[])
{
	if(!TrieKeyExists(g_tFiles[t_Accounts],key))
		return;
		
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	if(tData[ad_iHasAccess] || tData[ad_iHasBenefit])
		return;
	
	new iIndex = is_key_online(key);
	
	if(iIndex)
		g_iData[iIndex][ud_bAccount] = false;
	
	TrieDeleteKey(g_tFiles[t_Accounts],key);
	ArrayDeleteKey(g_aFiles[a_Accounts],key);
	
	//log_to_file(g_szLogFile,"[%s] empty account has been deleted. (%s - %s)",PLUGIN,get_key_name(key),key);
}

stock cmd_CreateAccount(const key[],const creatorKey[],const note[])
{
	if(TrieKeyExists(g_tFiles[t_Accounts],key))
		return;
		
	new tData[AccountData];
	
	formatex(tData[ad_szCreatorKey],charsmax(tData[ad_szCreatorKey]),creatorKey);
	formatex(tData[ad_szNote],charsmax(tData[ad_szNote]),note);
	
	ArrayPushString(g_aFiles[a_Accounts],key);
	TrieSetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);

	new iIndex = is_key_online(key);
	
	g_iData[iIndex][ud_bAccount] = true;
	
	for(new i = 1; i <= MaxClients; i++)
		if(!is_user_connected(i))
			continue;
		else if(i == iIndex)
			client_print_color(i,print_team_default,"^4[%s] ^1Your account has been created.",PREFIX);
		else
			client_print_color(i,print_team_default,"^4[%s] ^3%s^1's ^1account has been created.",PREFIX,get_name(iIndex));
		
	log_to_file(g_szLogFile,"[%s] new account has been created. (%s - %s)",PLUGIN,get_key_name(key),key);
}

stock cmd_LoadPrivilegeTime(const iIndex, const key[], const privilege)
{
	new iTime;
	
	if(privilege == p_Access)
	{
		new tData[AccessPrivilege];
		TrieGetArray(g_tFiles[t_AccessPrivileges],key,tData, sizeof tData);
		iTime = tData[ap_iExpired];
	}
	
	else
	{
		new tData[BenefitPrivilege];
		TrieGetArray(g_tFiles[t_BenefitPrivileges],key,tData, sizeof tData);
		iTime = tData[bp_iExpired];
	}
	
	
	new TimeLeft = iTime - get_systime();
	
	cmd_ResetPrivilegeTime(iIndex);
		
	if(iTime == PERMANENT)
	{
		g_iPrivilegeData[iIndex][pd_bPerm] = true;
		return;
	}
	
	TimeLeft ++;
	
	new iMultiTime[] = {60,3600,3600*24,3600*24*30,3600*24*365};
	
	if(TimeLeft < 60)
		g_iPrivilegeData[iIndex][pd_iMinutes] ++;
	
	for(new i = pd_iYears,j=t_Years; i >= pd_iMinutes; i--,j--)
		while(TimeLeft > iMultiTime[j])
		{
			g_iPrivilegeData[iIndex][i] ++;
			TimeLeft -= iMultiTime[j];
		}
}

stock cmd_ResetPrivilegeTime(const iIndex)
{
	for(new i = pd_iYears; i >= pd_iMinutes; i--)
		g_iPrivilegeData[iIndex][i] = 0;
		
	g_iPrivilegeData[iIndex][pd_bPerm] = false;
}

stock ArrayDeleteKey(&Array:aArray, const key[])
{
	for(new i = 0,szKey[25]; i < ArraySize(aArray); i++)
	{
		ArrayGetString(aArray,i,szKey,charsmax(szKey));
		if(equali(szKey,key))
		{
			ArrayDeleteItem(aArray,i);
			break;
		}
	}
}

stock is_valid_note(const note [])
{
	if(strlen(note) < NOTE_MINUMIM_LENGTH)
		return false;
	
	return true;
}

stock is_valid_time(const message[]/*, const timetype*/)
{
	if(!is_str_num(message))
		return false;
		
	return true;
}

stock get_name(const iIndex)
{
	new szName[32];
	get_user_name(iIndex,szName,charsmax(szName));
	
	return szName;
}

stock get_key(const iIndex)
{
	new szKey[25];
	get_user_authid(iIndex,szKey,charsmax(szKey));
	
	if(containi(szKey,"VALVE") != -1 || containi(szKey,"ID_LAN") != -1)
		get_user_ip(iIndex,szKey,charsmax(szKey),1);
	
	return szKey;
}

stock get_ip(const iIndex)
{
	new szIP[16];
	get_user_ip(iIndex,szIP,charsmax(szIP),1);
	
	return szIP;
}


stock get_key_last_seen(const key[])
{
	new szTemp[32];
	new iTemp = get_key_last_connected(key);
	
	switch(iTemp)
	{
		case CURRENTLY_ONLINE: // online
			formatex(szTemp,charsmax(szTemp),"Currently online");
		case NEVER_SEEN: // never connected
			formatex(szTemp,charsmax(szTemp),"Never seen");
		default:
			format_time(szTemp,charsmax(szTemp),"%d/%m/%Y %H:%M:%S", iTemp);
	}
	return szTemp;
}

stock get_date(const iTime)
{
	new szTemp[32];
	format_time(szTemp,charsmax(szTemp),"%d/%m/%Y %H:%M:%S", iTime);
	
	return szTemp;
}

stock get_key_privileges(const key[])
{
	new szTemp[128],iLen;
	
	new tData[AccountData];
	TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
	
	if(!tData[ad_iHasAccess] && !tData[ad_iHasBenefit])
		formatex(szTemp,charsmax(szTemp),"\d- \yHas no privileges\d.^n");
	else
	{
		if(tData[ad_iHasAccess])
			iLen += formatex(szTemp[iLen],charsmax(szTemp) - iLen,"\d- \yAccess Privilege: \r%s\d.^n",get_access_name(tData[ad_iHasAccess]));
		
		if(tData[ad_iHasBenefit])
			iLen += formatex(szTemp[iLen],charsmax(szTemp) - iLen,"\d- \yBenefit Privilege: \r%s\d.^n",get_benefit_name(tData[ad_iHasBenefit]));
	}
	
	return szTemp;
}

stock get_key_note(const key[])
{
	new szNote[32];
	
	if(TrieKeyExists(g_tFiles[t_Accounts],key))
	{
		new tData[AccountData];
		TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
		
		formatex(szNote,charsmax(szNote),tData[ad_szNote]);
	}
	
	return szNote;
}

stock get_key_creator(const key[])
{
	new szCreator[32];
	
	if(TrieKeyExists(g_tFiles[t_Accounts],key))
	{
		new tData[AccountData];
		TrieGetArray(g_tFiles[t_Accounts],key,tData,sizeof tData);
		
		formatex(szCreator,charsmax(szCreator),tData[ad_szCreatorKey]);
	}
	
	return szCreator;
}

stock get_user_privilege_length(const iIndex)
{
	new szTemp[64],iLen;
	
	if(g_iPrivilegeData[iIndex][pd_bPerm])
		iLen += formatex(szTemp,charsmax(szTemp),"Permanent");
	else
	{
		new const szTime[][] = {"Minute","Hour","Day","Month","Year"};
		
		for(new i = pd_iMinutes; i <= pd_iYears; i++)
			if(g_iPrivilegeData[iIndex][i] > 0)
				iLen += formatex(szTemp[iLen],charsmax(szTemp) - iLen,"%s\w%d \r%s%s",iLen > 0 ? "\d,\r" : "",g_iPrivilegeData[iIndex][i],szTime[i-pd_iMinutes],g_iPrivilegeData[iIndex][i] > 1 ? "s" : "");
	}
	
	if(iLen <= 0)// BRB
		formatex(szTemp,charsmax(szTemp),"Time has not set yet");
	
	return szTemp;
}

stock get_user_privilege_time(const iIndex)
{
	if(g_iPrivilegeData[iIndex][pd_bPerm])
		return PERMANENT;
	
	new iBonusTime = get_systime();
	
	new iMultiTime[Time] = {60,3600,3600*24,3600*24*30,3600*24*30*365};
	
	for(new i = pd_iMinutes; i <= pd_iYears; i++)
		iBonusTime += g_iPrivilegeData[iIndex][i] * iMultiTime[i-pd_iMinutes];
	
	return iBonusTime;
}

stock get_privilege_expired(const iTime,bool:bMenu)
{
	new TimeLeft = iTime - get_systime();
	
	new szTemp[128];
	
	if(iTime == PERMANENT)
	{
		if(bMenu)
			formatex(szTemp,charsmax(szTemp),"\rPermanent");
		else
			formatex(szTemp,charsmax(szTemp),"^3Permanent^1");
		return szTemp;
	}
	
	TimeLeft ++;
	
	
	new const szTime[][] = {"minute","hour","day","month","year"};
	
	new iMultiTime[] = {60,3600,3600*24,3600*24*30,3600*24*365};
	
	new aTime[Time];
	
	if(TimeLeft < 60)
		aTime[t_Minutes] ++;
	
	for(new i = Time - 1; i >= 0; i--)
		while(TimeLeft > iMultiTime[i])
		{
			aTime[i] ++;
			TimeLeft -= iMultiTime[i];
		}
	
	new iLen;
	
	for(new i = Time - 1; i >= 0; i--)
	{
		if(iLen > 1)
			break;
		if(aTime[i] > 0)
		{
			if(bMenu)
				iLen += formatex(szTemp[iLen], charsmax(szTemp) - iLen, "\d%s\r%d \w%s%s",iLen > 0 ? ", " : "", aTime[i], szTime[i], aTime[i] > 1 ? "s" : "");
			else
				iLen += formatex(szTemp[iLen], charsmax(szTemp) - iLen, "^4%s^1%d ^3%s%s^1",iLen > 0 ? ", " : "", aTime[i], szTime[i], aTime[i] > 1 ? "s" : "");
		}
	}
	
	trim(szTemp);
	return szTemp;
}

stock cmd_CreateOwnerAccount()
{
	if(!TrieKeyExists(g_tFiles[t_Accounts],g_szOwnerKey))
		cmd_CreateAccount(g_szOwnerKey,"loopback","Created by the system");
	
	new tData[AccountData];
	
	TrieGetArray(g_tFiles[t_Accounts],g_szOwnerKey,tData,sizeof tData);
	
	if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],g_szOwnerKey) || tData[ad_iHasAccess] < ServerManager)
	{			
		tData[ad_iHasAccess] = ServerManager;
		
		TrieSetArray(g_tFiles[t_Accounts],g_szOwnerKey,tData,sizeof tData);
		
		new tData2[AccessPrivilege];
		
		tData2[ap_iAccess] = ServerManager;
		tData2[ap_iExpired] = PERMANENT;
		tData2[ap_iLastEdit] = get_systime();
		formatex(tData2[ap_szEditor],charsmax(tData2[ap_szEditor]),"loopback");
	
		if(!TrieKeyExists(g_tFiles[t_AccessPrivileges],g_szOwnerKey))
			ArrayPushString(g_aFiles[a_AccessPrivileges],g_szOwnerKey);
			
		TrieSetArray(g_tFiles[t_AccessPrivileges],g_szOwnerKey,tData2,sizeof tData2);
	}
	
	new iIndex = is_key_online(g_szOwnerKey);
	
	if(iIndex)
	{
		g_iData[iIndex][ud_bAccount] = true;
		set_user_privilege(iIndex,p_Access, ServerManager);
	}
}

stock is_key_online(const key[])
{
	if(containi(key,"STEAM") == -1)
		return find_player("dh",key);
		
	return find_player("ch",key);
}
