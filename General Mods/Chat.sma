/* Plugin generated by AMXX-Studio */

#include "amxmodx.inc"
#include "cstrike.inc"
#include "accounts.inc"

#define PLUGIN "Chat System"
#define VERSION "1.0"
#define AUTHOR "MJ"

#define CHAT_DEAD_ACCESS Player
#define CHAT_ALIVE_ACCESS Player
#define CHAT_TEAM_ACCESS GameMaster
#define CHAT_GREEN_ACCESS ServerManager

#define CHAT_PREMIUM_TAG_ACCESS BenefitLv1
#define CHAT_PREMIUM_TAG "^4"

new const Float:g_fPostions[][] =
{
	{0.05,0.55},
	{-1.0, 0.2},
	{-1.0, 0.7},
};


#define PREFIX "Chat System"

new const g_szFile[] = "addons/amxmodx/data/chat_servermessage.txt";

new g_szServerMessage[192];

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);
	
	if(file_exists(g_szFile))
		LoadFileForMe(g_szFile,g_szServerMessage,charsmax(g_szServerMessage));
	else
		formatex(g_szServerMessage,charsmax(g_szServerMessage),"There is no server message yet");
	
	register_event("HLTV","event_OnNewRound", "a", "1=0", "2=0");
	
	register_clcmd("say","SayHandler",false);
	register_clcmd("say_team","SayHandler",true);
}

public event_OnNewRound()
	cmd_ShowServerMessage(0);

public client_putinserver(iIndex)
	set_task(2.0,"cmd_ShowServerMessage",iIndex);
	
public SayHandler(const iIndex, const bool:bTeam)
{
	static szMessage[192],szName[MAX_NAME_LENGTH],iAccess;
	
	szMessage[0] = EOS;
	
	read_args(szMessage,charsmax(szMessage));
	remove_quotes(szMessage);
	
	trim(szMessage);
	
	get_user_name(iIndex,szName,charsmax(szName));
	
	iAccess = get_user_access(iIndex);
	
	switch(szMessage[0])
	{
		case EOS:
			return PLUGIN_HANDLED;
		
		case '/':
			return PLUGIN_CONTINUE;
		
		case '@':
		{
			if(iAccess < VIP)
			{
				client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
				
				return PLUGIN_HANDLED;
			}
			
			new iCount = 1;
			
			while (szMessage[iCount] == '@')
				iCount++;
			
			if(iCount > sizeof g_fPostions)
				iCount = 1;
				
			for(new i; i < iCount; i++)
				replace(szMessage,charsmax(szMessage),"@","");
			
			trim(szMessage);
			
			if(szMessage[0] == EOS)
				return PLUGIN_HANDLED;
			
			iCount --;
	
			static iChannel[3];
			
			iChannel[iCount] ++;
			
			if(iChannel[iCount] >= 4)
				iChannel[iCount] = 0;
								
			set_hudmessage(255, 255, 255, g_fPostions[iCount][0], g_fPostions[iCount][1] + float(iChannel[iCount] + 3) / 35.0, 0, 6.0, 12.0,0.5, 0.15,iChannel[iCount]);
			
			show_hudmessage(0, "%s :  %s",szName,szMessage);
				
			client_print_color(0,iIndex,"^4[ ^1%s ^4] ^3%s ^1: ^4%s",get_access_name(iAccess),szName,szMessage);
			
			return PLUGIN_HANDLED;
			//cmd_AdminSay(iIndex,szMessage);
		}
		
		case '#':
		{
			if(iAccess < VIP)
			{
				client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
				
				return PLUGIN_HANDLED;
			}
			
			replace(szMessage,charsmax(szMessage),"#","");
			
			trim(szMessage);
			
			if(szMessage[0] == EOS)
				return PLUGIN_HANDLED;
				
			if(get_user_access(iIndex) >= CHAT_GREEN_ACCESS)
				format(szMessage,charsmax(szMessage),"^4%s",szMessage);
			
			for(new i = 1; i <= MaxClients; i++)
			{
				if(!is_user_connected(i))
					continue;
					
				if(get_user_access(i) < VIP)
					continue;
					
				client_print_color(i,iIndex,"^4[Administrator Chat] ^1%s - ^3%s ^1: %s",get_access_name(iAccess),szName,szMessage);
			}
		}
		
		case '$':
		{
			if(iAccess < ServerManager)
			{
				client_print_color(iIndex,print_team_default,"^4[%s] ^1You have no access to this command.",PREFIX);
				
				return PLUGIN_HANDLED;
			}
			
			replace(szMessage,charsmax(szMessage),"$","");
			
			trim(szMessage);
			
			if(szMessage[0] == EOS)
				return PLUGIN_HANDLED;
			
			copy(g_szServerMessage,charsmax(g_szServerMessage),szMessage);
			
			delete_file(g_szFile);
			write_file(g_szFile,g_szServerMessage);
			
			client_print_color(0,print_team_default,"^4[%s] ^3%s ^1has updated the server message information.",PREFIX,szName);
			
			cmd_ShowServerMessage(0);
		}
		
		default:
		{
			static i,iLen,iTeam,szEffects[64];
			
			szEffects[0] = EOS;
			iLen = 0;
			iTeam = cs_get_user_team(iIndex);
			
			if(!is_user_alive(iIndex))
				iLen += copy(szEffects,charsmax(szEffects),"*DEAD* ");
				
			if(bTeam)
			{
				static const szTeams[][] = {"None", "Terrorists", "Conter-Terrorists", "Spectators"};
				
				iLen  += formatex(szEffects[iLen],charsmax(szEffects) - iLen,"(%s) ",szTeams[iTeam]);
			}
			
			if(get_user_access(iIndex) >= CHAT_GREEN_ACCESS)
				format(szMessage,charsmax(szMessage),"^4%s",szMessage);
			
			iLen  += formatex(szEffects[iLen],charsmax(szEffects) - iLen,"%s^3%s ^1:",get_user_premium(iIndex) >= CHAT_PREMIUM_TAG_ACCESS ? CHAT_PREMIUM_TAG : "",szName);
			
			for(i = 1; i <= MaxClients; i++)
			{
				if(!is_user_connected(i))
					continue;
					
				if(!bTeam || cs_get_user_team(i) == iTeam || get_user_access(i) >= CHAT_TEAM_ACCESS)
				{
					if(is_user_alive(i) == is_user_alive(iIndex) || (!is_user_alive(iIndex) && get_user_access(i) >= CHAT_DEAD_ACCESS) || (is_user_alive(iIndex) && get_user_access(i) >= CHAT_ALIVE_ACCESS))
					{
						client_print_color(i,iIndex,"%s %s",szEffects,szMessage);
					}
				}
			}
		}
	}
	
	return PLUGIN_HANDLED_MAIN;
}

public cmd_ShowServerMessage(iIndex)
	client_print_color(iIndex,print_team_default,"^4[%s] ^3Server Message: ^1%s.",PREFIX,g_szServerMessage);
